uniform sampler2D framebuf;
uniform int left_eye;
uniform float kappa_0;
uniform float kappa_1;
uniform float kappa_2;
uniform float kappa_3;
uniform vec2 LensCenter;
uniform vec2 Scale;
uniform vec2 ScaleIn;

varying vec2 vPos;
varying vec2 vTex;

void main()
{

    vec2 theta = (vTex - LensCenter) * ScaleIn; 
    float rSq = theta.x * theta.x + theta.y * theta.y;
    vec2 rvector = theta * (kappa_0 + kappa_1 * rSq + kappa_2 * rSq * rSq + kappa_3 * rSq * rSq * rSq);
    vec2 tc = LensCenter + Scale * rvector;

    //keep within bounds of texture 
    if ((left_eye == 1 && (tc.x < 0.0 || tc.x > 0.5)) ||   
        (left_eye == 0 && (tc.x < 0.5 || tc.x > 1.0)) ||
        tc.y < 0.0 || tc.y > 1.0) {
        discard;
    }

    //keep on either side of viewport
    if (left_eye == 1 && vPos.x > 0.0) {
        discard;
    }
    else if (left_eye == 0 && vPos.x < 0.0) {
        discard;
    }


    gl_FragColor=texture2D(framebuf, tc); 

}

